plugins {
    id 'java'
    id 'jacoco'
    id 'application'
}

group 'fr.haizen.wordle'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    /**
     * Unit test dependencies
     */
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'

    /**
     * CLI Colors dependency
     */
    implementation 'com.diogonunes:JColor:5.5.1'

    /**
     * Lombok dependency (getters, setters, constructors, etc.)
     */
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'fr.haizen.wordle.WordleCLI'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.haizen.wordle.WordleCLI'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
